#compdef manage-local-mk

_manage_local_mk_list_all_presets()
{
    _manage_local_mk_all_presets=( $(manage-local-mk list | { while read a; do echo -E - " $a"; done; }) )
}

(( $+functions[_manage_local_mk_edit] )) || _manage_local_mk_edit()
{
    _arguments -w -S -s \
        '1: :->preset'

    if [[ "$state" == preset ]]; then
        _manage_local_mk_list_all_presets
        compadd -a _manage_local_mk_all_presets
    fi
}

(( $+functions[_manage_local_mk_install] )) || _manage_local_mk_install()
{
    _arguments -w -S -s \
        '1: :->preset' \
        '2: :_dirs'

    if [[ "$state" == preset ]]; then
        _manage_local_mk_list_all_presets
        compadd -a _manage_local_mk_all_presets
    fi
}

(( $+functions[_manage_local_mk_clean] )) || _manage_local_mk_clean()
{
    _arguments -w -S -s \
        '1: :_dirs'
}

(( $+functions[_manage_local_mk_scaffold] )) || _manage_local_mk_scaffold()
{
    _arguments -w -S -s \
        '1:preset:' \
        '2:pkgdir:_dirs' \
        '3:srcdir:_dirs'
}

(( $+functions[_manage_local_mk_command] )) || _manage_local_mk_command()
{
    local -a _manage_local_mk_cmds
    _manage_local_mk_cmds=(
        "list:list available presets"
        "edit:edit a local.mk preset"
        "install:install a local.mk preset"
        "clean:remove local.mk from directory"
        "scaffold:create a preset for some packages"
    )
    if (( CURRENT == 1 )); then
        _describe -t commands 'manage-local-mk command' _manage_local_mk_cmds || compadd "$@"
    else
        local curcontext="$curcontext"

        cmd="${${_manage_local_mk_cmds[(r)$words[1]:*]%%:*}}"

    if (( $#cmd )); then
            curcontext="${curcontext%:*:*}:manage-local-mk-${cmd}:"

            _call_function ret _manage_local_mk_$cmd || _message 'no more arguments'
        else
            _message "unknown manage-local-mk command: $words[1]"
        fi
        return ret
    fi
}

_arguments -w -S -s \
    '(-v --version)'{-v,--version}'' \
    '*::manage-local-mk command:_manage_local_mk_command'

# vim: ts=4 sts=4 sw=4 et ai
